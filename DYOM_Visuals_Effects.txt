
jump @DYOM_VISUALS_EFFECTS_END


// load params from config or apply defaults
// arg 0@ - objective idx, -2 to reset defaults 
:EFFECTS_CONFIG_UPDATE_PARAMS
    cleo_call @CONFIG_READ_PARAM args 4 0@ 0.0 true 'nightVision' result 1@
    0092: 1@ = float 1@ to_integer
    08B1: enable_night_vision 1@
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 0.0 true 'thermalVision' result 1@
    0092: 1@ = float 1@ to_integer
    08B2: enable_thermal_vision 1@
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ -1.0 false 'speed' result 1@
    if
        1@ <> -1.0
    then
        015D: set_gamespeed 1@
    end
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ -1.0 false 'fov' result 1@
    if
        1@ <> -1.0
    then
        0922: set_camera_zoom_from 1@ to 1@ timelimit 1000 smooth_transition 0
        0931: lock_camera_zoom 1
    else
        0931: lock_camera_zoom 0
    end
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 0.0 true 'forceCinematic' result 1@
    0092: 1@ = float 1@ to_integer
    093D: lock_camera_on_cinematic_view 1@
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 0.0 true 'forceBumper' result 1@
    0092: 1@ = float 1@ to_integer
    0085: forceBumper = 1@
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 0.0 true 'forceWalking' result 1@
    0092: 1@ = float 1@ to_integer
    0085: forceWalking = 1@
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 0.0 true 'forceAiming' result 1@
    0092: 1@ = float 1@ to_integer
    0085: forceAiming = 1@
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 0.0 true 'darkness' result 1@
    0092: 1@ = float 1@ to_integer
    0924: enable_screen_darkness 1@ with_value -1            
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ -1.0 false 'fadeout' result 1@
    if
        1@ <> -1.0
    then
        0092: 1@ = float 1@ to_integer
        016A: fade 0 time 1@
    end
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ -1.0 false 'fadein' result 1@
    if
        1@ <> -1.0
    then
        0092: 1@ = float 1@ to_integer
        016A: fade 1 time 1@
    end
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 0.0 false 'shake' result 1@
    0092: 1@ = float 1@ to_integer
    0003: shake_camera 1@
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 0.0 true 'siteorkcet' result 1@
    0092: 1@ = float 1@ to_integer
    09A3: show_siterocket_on_bumper_camera 1@ 
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 1.0 true 'radar' result 1@
    0092: 1@ = float 1@ to_integer
    0581: enable_radar 1@
    
    cleo_call @CONFIG_READ_PARAM args 4 0@ 1.0 true 'hud' result 1@
    0092: 1@ = float 1@ to_integer
    0826: enable_hud 1@
    09B9: show_entered_car_name 1@
    09BA: show_entered_zone_name 1@   
return


// once peer frame effects update
:EFFECTS_PROCESS
    if
        forceAiming == true
    then
        cleo_call @FORCE_KEYPRESS 1 args 12    
    end
    
    if
        forceWalking == true
    then
        cleo_call @FORCE_KEYPRESS 1 args 42
    end
    
    if
        forceBumper == true
    then
        09AD: set_vehicle_camera_mode 0
    end    
return


:FORCE_KEYPRESS
    0@ += 0xB734D0 // High Priority Controls
    0A8C: write_memory 0@ size 1 value 255 virtual_protect 0
cleo_return 0


:DYOM_VISUALS_EFFECTS_END
