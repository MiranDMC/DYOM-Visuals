
jump @DYOM_VISUALS_SHADERS_END // do not execute any code from this file yet


:SHADERS_INIT
    set_current_directory 0 // game dir
    if
        hShader = shader.Load("cleo/dyom_shaders/shaders/_main.hlsl")
    then
    else
        0AD1: show_formatted_text_highpriority "~r~~h~Failed to load DYOM Visuals shader!" time 6000
        0A93: terminate_this_custom_script
    end
        
    // have to be used at least once to be able to set parameters
    shader.Draw(hShader, "draw", 0.0, 0.0, 0.0, 0.0)
        
    // get screen X size
    read_memory 0@ = read_memory 0x00C17044 size 4 virtual_protect 0
    0093: 0@ = integer 0@ to_float
    shader.SetFloat(hShader, "screenWidth", 0@)
        
    // get screen Y size
    read_memory 0@ = read_memory 0x00C17048 size 4 virtual_protect 0
    0093: 0@ = integer 0@ to_float
    shader.SetFloat(hShader, "screenHeight", 0@)
return


// calculate and send peer frame contstants to the shaders
// arg 0 - shader handle
:SHADERS_CONSTANTS_UPDATE
    0093: 1@ = integer TimerA to_float
    1@ /= 1000.0
    shader.SetFloat(0@, "timeMission", 1@)

    0093: 1@ = integer TimerB to_float
    1@ /= 1000.0
    shader.SetFloat(0@, "timeObjective", 1@)
    
    008F: 1@ = integer $10912 to_float
    1@ /= 1000.0
    shader.SetFloat(0@, "timeLeft", 1@)

    read_memory 1@ = read_memory 0x00B76848 size 4 virtual_protect 0
    shader.SetFloat(0@, "drawDistance", 1@)
cleo_return 0


// arg 0 - shader handle
:SHADERS_DRAW
    shader.Draw(0@, "draw", SCREEN_BASE_X, SCREEN_BASE_Y, SCREEN_SIZE_X, SCREEN_SIZE_Y)
cleo_return 0


// load parameters from config or apply defaults
// arg 0 - shader handle
// arg 1 - objective idx, -1 use editor.ini, -2 just set default param
// return - (bool) true if setup completed for this objective
:SHADERS_CONFIG_UPDATE_PARAMS
    2@ = 1.0 // transition progress
    
    if
        1@ >= 0
    then
        if
            cleo_call @CONFIG_EXISTS args 0
        then
            cleo_call @CONFIG_READ_PARAM args 4 1@ 0.0 false 'transition' result 3@
            if and
                3@ > 0.0 // transition time defined
                1@ > 1 // not first objective or preview mode
            then
                0093: 4@ = integer TimerB to_float
                4@ /= 1000.0
                
                if
                    8035: 4@ < 3@ // transition still in progress
                then
                    0087: 2@ = 4@
                    0073: 2@ /= 3@ // 0-1 range progress
                end
            end 
        else
            1@ = -2 // mission effect file not exists, just set default param
        end
    end   
        
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'brightness'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'contrast'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'saturation'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'tint'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'hue'
    
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'fogRed'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'fogGreen'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'fogBlue'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'fogAlpha'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'fogStart'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'fogEnd'
    
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'inputMinRed'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'inputMaxRed'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'outputMinRed'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'outputMaxRed'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'inputMinGreen'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'inputMaxGreen'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'outputMinGreen'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'outputMaxGreen'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'inputMinBlue'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'inputMaxBlue'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'outputMinBlue'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'outputMaxBlue'
    
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'distort'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'rainDrops'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'flipY'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'flipX'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'barsWidth'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'barsHeight'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'glitch'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.1 2@ 'glitchY'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.1 2@ 'glitchXs'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'zoom'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'quantize'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'oneColor'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'interlaces'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'desolate'
        
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.5 2@ 'vignetteRound' 
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 2.0 2@ 'vignetteStart'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 3.0 2@ 'vignetteWidth'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'vignetteAspect'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'vignetteRed'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'vignetteGreen'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'vignetteBlue'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 1.0 2@ 'vignetteAlpha'
    cleo_call @SHADERS_CONFIG_READ_PARAM args 5 0@ 1@ 0.0 2@ 'vignetteMixing'
    
    if
        2@ >= 1.0 // transition done
    then
        cleo_return 1 true
    end
cleo_return 1 false


// arg 0 - shader handle
// arg 1 - objective idx, -1 use editor.ini, -2 just set default param
// arg 2 - (float) default parameter value
// arg 3 - (float) transition time in seconds 
// arg 4 - parameter name
:SHADERS_CONFIG_READ_PARAM
    if
        1@ == -2 // just apply default
    then
        string_format 7@v = string_format "%s" 4@ // copy param name into local variables
        shader.SetFloat(0@, 7@v, 2@)
        cleo_return 0
    end

    0AC8: 5@ = allocate_memory_size 16
    string_format 5@ = string_format "%s" 4@ // copy param name into memory with static address

    cleo_call @CONFIG_READ_PARAM args 4 1@ 2@ true 5@ result 6@
    
    if
        3@ < 1.0 // transition time defined
    then
        1@ -= 1 // previous objective
        cleo_call @CONFIG_READ_PARAM args 4 1@ 2@ true 5@ result 7@
        cleo_call @FLOAT_LERP args 3 7@ 6@ 3@ result 6@
    end
        
    string_format 7@v = string_format "%s" 5@ // copy param name into local variables
    0AC9: free_allocated_memory 5@
    
    shader.SetFloat(0@, 7@v, 6@)
 cleo_return 0


:DYOM_VISUALS_SHADERS_END
